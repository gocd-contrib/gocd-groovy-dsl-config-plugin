/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

static String versionOf(String dependencySpec) {
  return dependencySpec.split(':').last()
}

final Map<String, String> libraries = [
  // Dependabot will parse these.
  // Keep plaintext versions here so Dependabot can parse the versions; don't interpolate version variables here
  // because Dependabot is not smart enough to understand those.
  ant               : 'org.apache.ant:ant:1.10.15',
  classgraph        : 'io.github.classgraph:classgraph:4.8.179',
  commonsLang       : 'org.apache.commons:commons-lang3:3.17.0',
  commonsText       : 'org.apache.commons:commons-text:1.13.0',
  dom4j             : 'org.dom4j:dom4j:2.1.4',
  glassfish         : 'org.glassfish:jakarta.el:4.0.2',
  groovy            : 'org.apache.groovy:groovy:4.0.26',
  guava             : 'com.google.guava:guava:33.4.5-jre',
  hibernateValidator: 'org.hibernate.validator:hibernate-validator:8.0.2.Final',
  jacksonBom        : 'com.fasterxml.jackson:jackson-bom:2.18.3',
  jcommander        : 'com.beust:jcommander:1.82',
  jhighlight        : 'org.codelibs:jhighlight:1.1.0',
  jsonFluent        : 'net.javacrumbs.json-unit:json-unit-fluent:4.1.0',
  junitJupiterBom   : 'org.junit:junit-bom:5.12.1',
  lombok            : 'org.projectlombok:lombok:1.18.36',
  mockito           : 'org.mockito:mockito-core:5.16.0',
  okio              : 'com.squareup.okio:okio:1.17.6',
  pluginApi         : 'cd.go.plugin:go-plugin-api:25.1.0',
  reflections       : 'org.reflections:reflections:0.10.2',
  retrofit          : 'com.squareup.retrofit2:retrofit:2.11.0',
  slf4j             : 'org.slf4j:slf4j-simple:2.0.17',
  validateApi       : 'jakarta.validation:jakarta.validation-api:3.1.1',
]

// Parse some versions that are shared with related deps; this way we can DRY and keep these adjunct deps
// up-to-date.
final String groovyVersion = versionOf(libraries.groovy)
final String hibernateVersion = versionOf(libraries.hibernateValidator)
final String retrofitVersion = versionOf(libraries.retrofit)

// RELATED: these deps share versions with others; we put these after the main deps because Dependabot cannot parse
// interpolated versions. When Dependabot updates the parent dependency, these will get updated for free too.
libraries.groovyJson = "org.apache.groovy:groovy-json:${groovyVersion}"
libraries.hibernateValidatorAnnotationProcessor = "org.hibernate.validator:hibernate-validator-annotation-processor:${hibernateVersion}"
libraries.retrofitJacksonConv = "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"

libraries.jacksonCore = "com.fasterxml.jackson.core:jackson-core"
libraries.jacksonAnnotations = "com.fasterxml.jackson.core:jackson-annotations"
libraries.jacksonDatabind = "com.fasterxml.jackson.core:jackson-databind"
libraries.junitJupiterApi = "org.junit.jupiter:junit-jupiter-api"
libraries.junitJupiterEngine = "org.junit.jupiter:junit-jupiter-engine"
libraries.junitJupiterPlatformLauncher = "org.junit.platform:junit-platform-launcher"
libraries.junitJupiterParams = "org.junit.jupiter:junit-jupiter-params"

// Export versions that are needed outside of this file
final Map<String, String> versions = [
  groovyVersion: groovyVersion
]

ext {
  //noinspection GroovyAssignabilityCheck
  deps = libraries
  //noinspection GroovyAssignabilityCheck
  vers = versions
}
