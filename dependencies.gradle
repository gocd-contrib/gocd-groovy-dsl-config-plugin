/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

static def versionOf(String dependencySpec) {
  return dependencySpec.split(':').last()
}

final Map<String, String> libraries = [
  // Dependabot will parse these.
  // Keep plaintext versions here so Dependabot can parse the versions; don't interpolate version variables here
  // because Dependabot is not smart enough to understand those.
  ant               : 'org.apache.ant:ant:1.10.8',
  classgraph        : 'io.github.classgraph:classgraph:4.8.90',
  commonsLang       : 'org.apache.commons:commons-lang3:3.11',
  commonsText       : 'org.apache.commons:commons-text:1.9',
  dom4j             : 'dom4j:dom4j:1.6.1',
  glassfish         : 'org.glassfish:javax.el:3.0.0',
  groovy            : 'org.codehaus.groovy:groovy:2.5.12',
  groovySandbox     : "org.kohsuke:groovy-sandbox:1.24",
  guava             : 'com.google.guava:guava:29.0-jre',
  hamcrestCore      : 'org.hamcrest:hamcrest-core:2.2',
  hibernateValidator: 'org.hibernate.validator:hibernate-validator:6.1.6.Final',
  ivy               : 'org.apache.ivy:ivy:2.5.0',
  jacksonCore       : 'com.fasterxml.jackson.core:jackson-core:2.11.2',
  jcommander        : 'com.beust:jcommander:1.78',
  jhighlight        : 'org.codelibs:jhighlight:1.0.3',
  jsonFluent        : 'net.javacrumbs.json-unit:json-unit-fluent:2.19.0',
  junitJupiterApi   : 'org.junit.jupiter:junit-jupiter-api:5.7.0',
  lombok            : 'org.projectlombok:lombok:1.18.12',
  mockito           : 'org.mockito:mockito-core:3.5.11',
  pluginApi         : 'cd.go.plugin:go-plugin-api:20.8.0',
  reflections       : 'org.reflections:reflections:0.9.12',
  retrofit          : 'com.squareup.retrofit2:retrofit:2.9.0',
  validateApi       : 'javax.validation:validation-api:2.0.1.Final',
]

// Parse some versions that are shared with related deps; this way we can DRY and keep these adjunct deps
// up-to-date.
final String groovyVersion = versionOf(libraries.groovy)
final String hamcrestVersion = versionOf(libraries.hamcrestCore)
final String hibernateVersion = versionOf(libraries.hibernateValidator)
final String jacksonVersion = versionOf(libraries.jacksonCore)
final String junitJupiterVersion = versionOf(libraries.junitJupiterApi)
final String retrofitVersion = versionOf(libraries.retrofit)

// RELATED: these deps share versions with others; we put these after the main deps because Dependabot cannot parse
// interpolated versions. When Dependabot updates the parent dependency, these will get updated for free too.
libraries.groovyAll = "org.codehaus.groovy:groovy-all:${groovyVersion}"
libraries.groovyDateUtil = "org.codehaus.groovy:groovy-dateutil:${groovyVersion}"
libraries.groovyJson = "org.codehaus.groovy:groovy-json:${groovyVersion}"
libraries.groovySql = "org.codehaus.groovy:groovy-sql:${groovyVersion}"
libraries.groovySwing = "org.codehaus.groovy:groovy-swing:${groovyVersion}"
libraries.groovyXml = "org.codehaus.groovy:groovy-xml:${groovyVersion}"
libraries.hamcrestLib = "org.hamcrest:hamcrest-library:${hamcrestVersion}"
libraries.hibernateValidatorAnnotationProcessor = "org.hibernate.validator:hibernate-validator-annotation-processor:${hibernateVersion}"
libraries.jacksonAnnotations = "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
libraries.jacksonDatabind = "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
libraries.junitJupiterEngine = "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
libraries.junitJupiterParams = "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
libraries.retrofitJacksonConv = "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"

// Export versions that are needed outside of this file
final Map<String, String> versions = [
  groovyVersion: groovyVersion
]

ext {
  //noinspection GroovyAssignabilityCheck
  deps = libraries
  //noinspection GroovyAssignabilityCheck
  vers = versions
}
