/*
 * Copyright 2022 Thoughtworks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

static String versionOf(String dependencySpec) {
  return dependencySpec.split(':').last()
}

final Map<String, String> libraries = [
  // Dependabot will parse these.
  // Keep plaintext versions here so Dependabot can parse the versions; don't interpolate version variables here
  // because Dependabot is not smart enough to understand those.
  ant               : 'org.apache.ant:ant:1.10.13',
  classgraph        : 'io.github.classgraph:classgraph:4.8.154',
  commonsLang       : 'org.apache.commons:commons-lang3:3.12.0',
  commonsText       : 'org.apache.commons:commons-text:1.10.0',
  dom4j             : 'org.dom4j:dom4j:2.1.3',
  glassfish         : 'org.glassfish:jakarta.el:4.0.2',
  groovy            : 'org.codehaus.groovy:groovy:3.0.14',
  guava             : 'com.google.guava:guava:31.1-jre',
  hibernateValidator: 'org.hibernate.validator:hibernate-validator:8.0.0.Final',
  jacksonCore       : 'com.fasterxml.jackson.core:jackson-core:2.14.1',
  jcommander        : 'com.beust:jcommander:1.82',
  jhighlight        : 'org.codelibs:jhighlight:1.1.0',
  jsonFluent        : 'net.javacrumbs.json-unit:json-unit-fluent:2.36.0',
  junitJupiterApi   : 'org.junit.jupiter:junit-jupiter-api:5.9.1',
  lombok            : 'org.projectlombok:lombok:1.18.24',
  mockito           : 'org.mockito:mockito-core:4.11.0',
  pluginApi         : 'cd.go.plugin:go-plugin-api:22.3.0',
  reflections       : 'org.reflections:reflections:0.10.2',
  retrofit          : 'com.squareup.retrofit2:retrofit:2.9.0',
  slf4j             : 'org.slf4j:slf4j-simple:2.0.6',
  validateApi       : 'jakarta.validation:jakarta.validation-api:3.0.2',
]

// Parse some versions that are shared with related deps; this way we can DRY and keep these adjunct deps
// up-to-date.
final String groovyVersion = versionOf(libraries.groovy)
final String hibernateVersion = versionOf(libraries.hibernateValidator)
final String jacksonVersion = versionOf(libraries.jacksonCore)
final String junitJupiterVersion = versionOf(libraries.junitJupiterApi)
final String retrofitVersion = versionOf(libraries.retrofit)

// RELATED: these deps share versions with others; we put these after the main deps because Dependabot cannot parse
// interpolated versions. When Dependabot updates the parent dependency, these will get updated for free too.
libraries.groovyJson = "org.codehaus.groovy:groovy-json:${groovyVersion}"
libraries.hibernateValidatorAnnotationProcessor = "org.hibernate.validator:hibernate-validator-annotation-processor:${hibernateVersion}"
libraries.jacksonAnnotations = "com.fasterxml.jackson.core:jackson-annotations:${jacksonVersion}"
libraries.jacksonDatabind = "com.fasterxml.jackson.core:jackson-databind:${jacksonVersion}"
libraries.junitJupiterEngine = "org.junit.jupiter:junit-jupiter-engine:${junitJupiterVersion}"
libraries.junitJupiterParams = "org.junit.jupiter:junit-jupiter-params:${junitJupiterVersion}"
libraries.retrofitJacksonConv = "com.squareup.retrofit2:converter-jackson:${retrofitVersion}"

// Export versions that are needed outside of this file
final Map<String, String> versions = [
  groovyVersion: groovyVersion
]

ext {
  //noinspection GroovyAssignabilityCheck
  deps = libraries
  //noinspection GroovyAssignabilityCheck
  vers = versions
}
