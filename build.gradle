/*
 * Copyright 2020 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

buildscript {
  ext {
    antVersion = '1.10.7'
    assertJVersion = '3.14.0'
    classgraphVersion = '4.8.64'
    commonsLangVersion = '3.10'
    commonsTextVersion = '1.8'
    dom4jVersion = '1.6.1'
    glassfishVersion = '3.0.0'
    groovySandboxVersion = '1.24'
    groovyVersion = '2.5.12'
    guavaVersion = '28.2-jre'
    hamcrestVersion = '2.2'
    hiberVersion = '6.1.1.Final'
    ivyVersion = '2.5.0'
    jacksonVersion = '2.10.2'
    jcommanderVersion = '1.78'
    jhighlightVersion = '1.0.3'
    jsonFluentVersion = '2.12.0'
    jupiterVersion = '5.6.2'
    lombokVersion = '1.18.10'
    mockitoVersion = '3.2.4'
    pluginApiVersion = '20.5.0'
    reflectionsVersion = '0.9.12'
    retrofitVersion = '2.7.2'
    validateApiVersion = '2.0.1.Final'
  }

  repositories {
    gradlePluginPortal()
    mavenCentral()
  }

  dependencies {
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
    classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.21.2"
  }
}

apply plugin: 'java'
apply plugin: 'com.bmuschko.nexus'
apply plugin: "io.codearte.nexus-staging"
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

gocdPlugin {
  id = 'cd.go.contrib.plugins.configrepo.groovy'
  pluginVersion = '1.1.0'
  goCdVersion = '20.6.0'
  name = 'GoCD Groovy Configuration plugin'
  description = 'GoCD pipelines and environments configuration in Groovy'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd-contrib/gocd-groovy-dsl-config-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-groovy-dsl-config-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project(':gocd-groovy-dsl-config-plugin')

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project(':gocd-groovy-dsl-config-plugin').tasks.jar, project(':dsl').tasks.jar]
}

allprojects {
  group = 'cd.go.groovydsl'
  version = gocdPlugin.fullVersion(project)

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
}

subprojects {
  repositories {
    mavenCentral()
    maven {
      url = 'https://repo.jenkins-ci.org/releases'
      content {
        includeModule "org.kohsuke", 'groovy-sandbox'
      }
    }
  }

}

nexusStaging {
  packageGroup = 'cd.go'
  username = System.getenv('GOCD_NEXUS_USERNAME')
  password = System.getenv('GOCD_NEXUS_PASSWORD')
}
