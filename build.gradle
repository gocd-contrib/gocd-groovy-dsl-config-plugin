/*
 * Copyright 2021 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
  id 'java'
  id 'io.github.gradle-nexus.publish-plugin' version '1.1.0'
}

apply from: 'dependencies.gradle'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

java {
  sourceCompatibility = JavaVersion.VERSION_11
  targetCompatibility = JavaVersion.VERSION_11
}

gocdPlugin {
  id = 'cd.go.contrib.plugins.configrepo.groovy'
  pluginVersion = '2.2.0'
  goCdVersion = '20.8.0'
  name = 'GoCD Groovy Configuration plugin'
  description = 'GoCD pipelines and environments configuration in Groovy'
  vendorName = 'ThoughtWorks, Inc.'
  vendorUrl = 'https://github.com/gocd-contrib/gocd-groovy-dsl-config-plugin'

  githubRepo {
    owner = System.getenv('GITHUB_USER') ?: 'bob'
    repo = 'gocd-groovy-dsl-config-plugin'
    token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  }

  pluginProject = project(':gocd-groovy-dsl-config-plugin')

  prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
  assetsToRelease = [project(':gocd-groovy-dsl-config-plugin').tasks.jar, project(':dsl').tasks.jar]
}

allprojects {
  group = 'cd.go.groovydsl'
  version = gocdPlugin.fullVersion(project)

  java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }

  tasks.withType(JavaCompile) {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
  }
}

subprojects {
  repositories {
    mavenCentral()
  }
}

nexusPublishing {
  packageGroup = 'cd.go'
  repositories {
    sonatype {
      username = System.getenv('GOCD_NEXUS_USERNAME')
      password = System.getenv('GOCD_NEXUS_PASSWORD')
    }
  }
}
