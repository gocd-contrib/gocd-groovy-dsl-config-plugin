/*
 * Copyright 2018 ThoughtWorks, Inc.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import org.eclipse.jgit.api.Git

buildscript {
  repositories {
    jcenter()
  }

  dependencies {
    classpath group: 'org.eclipse.jgit', name: 'org.eclipse.jgit', version: '5.1.3.201810200350-r'
    classpath 'com.bmuschko:gradle-nexus-plugin:2.3.1'
  }
}

plugins {
  id 'com.github.ben-manes.versions' version '0.20.0'
  id "com.bmuschko.nexus" version "2.3.1"
  id "co.riiid.gradle" version "0.4.2"
  id 'io.codearte.nexus-staging' version "0.11.0"
}

apply plugin: "io.codearte.nexus-staging"

allprojects {
  group = 'com.github.ketan'
  project.ext.pluginVersion = '0.5.0'
  project.ext.gitRevision = Git.open(rootProject.projectDir).log().setMaxCount(1).call().first().name()
  project.ext.distVersion = Git.open(rootProject.projectDir).log().call().size()
  project.ext.fullVersion = project.distVersion ? "${project.pluginVersion}-${project.distVersion}" : project.pluginVersion
  version = project.fullVersion
  project.ext.pluginDesc = [
    id         : 'cd.go.contrib.plugins.configrepo.groovy',
    version    : project.fullVersion,
    goCdVersion: '18.3.0',
    name       : 'GoCD Groovy Configuration plugin',
    description: 'GoCD pipelines and environments configuration in Groovy',
    vendorName : 'GoCD Contributors',
    vendorUrl  : 'https://github.com/ketan/gocd-groovy-dsl-config-plugin',
  ]

  tasks.withType(AbstractArchiveTask) {
    includeEmptyDirs false
    duplicatesStrategy DuplicatesStrategy.EXCLUDE

    preserveFileTimestamps = false
    reproducibleFileOrder = true
  }
}

subprojects {
  repositories {
    jcenter()
  }

  apply plugin: 'java'
  apply plugin: 'groovy'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  apply from: "${rootProject.projectDir}/plugin-common.gradle"
}

task allDependencies {
  dependsOn allprojects.collect { "$it.path:dependencies" }

  description = "Print dependency tree of all projects"
}

github {
  owner = System.getenv('GITHUB_USER') ?: 'bob'
  repo = 'gocd-groovy-dsl-config-plugin'
  token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
  tagName = "v${rootProject.pluginVersion}"
  name = "Version ${rootProject.pluginVersion}"
  targetCommitish = project.gitRevision

  def releaseNotesUrl = "https://github.com/${github.owner}/${github.repo}/blob/master/CHANGELOG.md#${github.tagName.replaceAll(/[^A-Za-z0-9]/, '')}"

  body = """
      # Version ${rootProject.fullVersion}
      
      See release notes at ${releaseNotesUrl}
      
      * The `gocd-groovy-dsl-config-plugin.jar` file contains the plugin.
      * The `dsl.jar` is the api jar containing the DSL interface. You
        can add this jar to your IDE to assist with autocompletion. See the
        README.md file for instructions on using this jar.
    """.stripIndent().trim()

  assets = project(':dsl').jar.outputs.files.files + project(':gocd-groovy-dsl-config-plugin').jar.outputs.files.files
}

githubRelease.dependsOn subprojects*.tasks*.findByName('assemble')
